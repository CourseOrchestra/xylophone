properties(
[
	pipelineTriggers([
		triggers: [
			 [
				  $class: 'hudson.triggers.SCMTrigger',
				  scmpoll_spec : 'H 0 * * 0'
			 ]
		]
  ])
]
)

node {
    def server = Artifactory.server 'ART'
    def rtMaven = Artifactory.newMavenBuild()
    def buildInfo
    def descriptor
    def releaseVersion

    stage ('Clone') {
        checkout scm
        sh '''git checkout dev'''

        result = sh (script: "git log -1 | grep '\\[ci skip\\]'", returnStatus: true)
        if (result == 0) {
            currentBuild.result = 'SUCCESS'
            return
        }
    }

    stage ('Artifactory configuration') {
        rtMaven.tool = 'M3'
        rtMaven.deployer releaseRepo: 'libs-release-local', snapshotRepo: 'libs-snapshot-local', server: server
        rtMaven.resolver releaseRepo: 'libs-release', snapshotRepo: 'libs-snapshot', server: server
        buildInfo = Artifactory.newBuildInfo()
        buildInfo.env.capture = true

        descriptor = Artifactory.mavenDescriptor()
        pom = readMavenPom file: 'pom.xml'
        releaseVersion = pom.version.split('-')[0]
        descriptor.version = releaseVersion
        descriptor.failOnSnapshot = true
        descriptor.transform()
    }

    stage ('Make release') {
        rtMaven.run pom: 'pom.xml', goals: '-DskipTests clean deploy -Ppublication,gpg', buildInfo: buildInfo
        server.publishBuildInfo buildInfo
    }

    stage ('Update repository') {
        sh '''git add .'''

        def commitReleaseScript = "git commit -m \"updating poms for " + releaseVersion + " release [ci skip]\""
        sh commitReleaseScript
        def tagScript = "git tag " + releaseVersion
        sh tagScript

        def splittedVersion = releaseVersion.split('\\.')
        splittedVersion[2] = (splittedVersion[2].toInteger() + 1) as String
        def newSnapshotVersion = splittedVersion.join('.') + '-SNAPSHOT'
        descriptor.version = newSnapshotVersion
        descriptor.failOnSnapshot = false
        descriptor.transform()

        sh '''git add .'''
        def commitSnapshotScript = "git commit -m \"updating poms for " + newSnapshotVersion + " development [ci skip]\""
        sh commitSnapshotScript

        sshagent(['cf816ae4-a98e-4eaa-98fd-18c588739711']) {
            sh '''git push origin dev'''
            sh '''git push --tags'''
        }
    }
}